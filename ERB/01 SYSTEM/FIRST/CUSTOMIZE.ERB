@startCustomization

	CLEARLINE 99999
	CALL customizeName
	CALL customizeHistory
	;캐릭터설정은 패스
	;CALL customizeCharacteristics
	;캐릭터 복장도 패스
	;CALL customizeClothes
	
	PRINTW 설정 완료
	PRINTW 여기에 대단한 튜토리얼 스토리가 진행될 예정입니다!

; ------------------
; 이름 및 호칭 설정(완료)
; ------------------
@customizeName

$inputNAME

PRINTL
PRINTL 플레이어의 이름을 입력해주세요.(기본 : 당신) 
;	PRINTL ※ 글자 수 2~12자 제한
PRINTL
INPUTS

IF strlens(RESULTS)
	NAME:MASTER = %RESULTS%
ELSE
	NAME:MASTER = 당신
ENDIF

PRINTL
PRINTL
PRINTFORML 플레이어의 이름은 %NAME:MASTER%%krPost(NAME:MASTER, "이", "가")% 맞습니까?
PRINTL
PRINTL

PRINTBUTTON "0[   네   ]  ", 0
PRINTBUTTON "1[ 아니오 ]", 1
PRINTL 

$INPUT_LOOP1
INPUT
IF RESULT == 0
	PRINTFORMW 이름을 %NAME:MASTER%%krPost(NAME:MASTER, "으로", "로")% 설정합니다.
ELSEIF RESULT == 1
	GOTO inputNAME
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP1
ENDIF

$inputCALLNAME

PRINTL
PRINTL 플레이어의 호칭을 입력해주세요.(기본: 당신)
PRINTL
INPUTS

IF strlens(RESULTS)
	CALLNAME:MASTER = %RESULTS%
ELSE
	CALLNAME:MASTER = 당신
ENDIF
	
PRINTL
PRINTL
PRINTFORML 플레이어의 호칭은 %CALLNAME:MASTER%%krPost(CALLNAME:MASTER, "이", "가")% 맞습니까?
PRINTL
PRINTL

PRINTBUTTON "0[   네   ]  ", 0
PRINTBUTTON "1[ 아니오 ]", 1
PRINTL 

$INPUT_LOOP2
INPUT
IF RESULT == 0
	PRINTFORMW 호칭을 %CALLNAME:MASTER%%krPost(CALLNAME:MASTER, "으로", "로")% 설정합니다.
ELSEIF RESULT == 1
	GOTO inputCALLNAME
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP2
ENDIF

	PRINTFORMW %NAME:MASTER%(%CALLNAME:MASTER%)%krPost(NAME:MASTER, "으로", "로")% 진행합니다.

; ---------------------
; 플레이어의 과거 설정(공사 중)
; ---------------------
@customizeHistory
#DIM LINEC
#DIM FIRST_LINE


$inputHistory
FIRST_LINE = LINECOUNT

PRINTL
PRINTL ***** 당신은 어떤 사람이었나요? *****
PRINTL
PRINTL [1] - 이세계에서 온 고등학생
PRINTL       불의의 사고로 이세계로 오게 된 평범한 고등학생
PRINTL       고등교육으로 지능이, 낯선 세계에 적응하기 위해 관찰이 높다.
PRINTL
PRINTL [2] - 거두어진 고아(추천)
PRINTL       전대 여관장에게 거둬 길러진 고아
PRINTL       어릴 적 부터 여관일을 많이 하여 응대와 청결이 높다.
PRINTL
PRINTL [3] - 몰락한 귀족 가문의 후계자
PRINTL       상속권을 잃은 채 도망쳐와 남은 돈으로 여관을 인수하였다.
PRINTL       경영 능력치와 초기 자본이 높다.
PRINTL



INPUT
;고등학생
IF RESULT == 1
	PRINTL [이세계에서 온 고등학생]으로 시작하겠습니까?
	PRINTL 
	PRINTL 특징 : 지능 + 10, 관찰 + 10, <스마트폰> 소지(아직 미구현)
	PRINTL 
	
	CALL ASK_YN
	IF RESULT == 0
		PRINTW [이세계에서 온 고등학생]으로 시작합니다.
		CFLAG:MASTER:태생 = 1
		ABL:MASTER:지능 += 10
		ABL:MASTER:관찰 += 10
		;스마트폰은 나중에 추가
	ELSE
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO inputHistory
	ENDIF

;고아
ELSEIF RESULT == 2
	PRINTL [거두어진 고아]로 시작하겠습니까?
	PRINTL 
	PRINTL 특징 : 응대 + 10, 청결 + 5, 조리 + 10, 마을사람의 호감도 증가
	PRINTL 
	
	CALL ASK_YN
	IF RESULT == 0
		PRINTW [거두어진 고아]로 시작합니다.
		CFLAG:MASTER:태생 = 2
		ABL:MASTER:조리 += 10
		ABL:MASTER:청결 += 5
		ABL:MASTER:응대 += 10
		;마을사람 호감도 증가는 미구현
	ELSE
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO inputHistory
	ENDIF

ELSEIF RESULT == 3
	PRINTL [몰락한 귀족 가문의 후계자]로 시작하겠습니까?
	PRINTL 
	PRINTL 특징 : 경영 + 10, 인내 + 10, 시작 골드 + 500G
	PRINTL 
	
	CALL ASK_YN
	IF RESULT == 0
		PRINTW [몰락한 귀족 가문의 후계자]로 시작합니다.
		CFLAG:MASTER:태생 = 3
		ABL:MASTER:전투기술 += 10
		ABL:MASTER:경영 += 10
		ABL:MASTER:인내 += 10
		MONEY += 500
	ELSE
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO inputHistory
	ENDIF

ELSE
	CALL systemError("잘못된 입력입니다")
	CLEARLINE 2
	GOTO inputHistory
ENDIF

@customizeCharacteristics

	PRINTL
	PRINTL 성격 선택(대사와 일부 게임플레이에 영향을 미칩니다)
	PRINTL [1] - 순진한 성격
	PRINTL [2] - 발랄한 성격
	PRINTL [3] - 요염한 성격
	PRINTL [4] - 음침한 성격
	PRINTL [5] - 쿨한 성격

$inputCharacteristics

	input

	if 1 <= RESULT && RESULT <= 5
		CFLAG:MASTER:성격 = RESULT

		if CFLAG:MASTER:태생 == 2 && (RESULT == 1 || RESULT == 2)
			CALL systemWarning("주의 : 해당 설정은 이전의 태생 설정과 충돌하는 경향이 있습니다. 플레이 시 참고하시기 바랍니다.")
		elseif CFLAG:MASTER:태생 == 3 && RESULT == 1
			CALL systemWarning("주의 : 해당 설정은 이전의 태생 설정과 충돌하는 경향이 있습니다. 플레이 시 참고하시기 바랍니다.")
		elseif CFLAG:MASTER:태생 == 5 && (RESULT == 3 || RESULT == 4)
			CALL systemWarning("주의 : 해당 설정은 이전의 태생 설정과 충돌하는 경향이 있습니다. 플레이 시 참고하시기 바랍니다.")
		endif
		
		if RESULT == 1
			CFLAG:MASTER:대사색상 = 0xb9fc00
			CALL speak(MASTER, "안녕하세요. 잘 부탁드려요.")
		elseif RESULT == 2
			CFLAG:MASTER:대사색상 = 0xfca400
			CALL speak(MASTER, "모험이네요! 좋죠!")
		elseif RESULT == 3
			CFLAG:MASTER:대사색상 = 0xff7aba
			CALL speak(MASTER, "드디어 시작되는 건가?")
		elseif RESULT == 4
			CFLAG:MASTER:대사색상 = 0xaa55ff
			CALL speak(MASTER, "밖으로 나가는건... 좀 힘든데.")
		elseif RESULT == 5
			CFLAG:MASTER:대사색상 = 0x008cff
			CALL speak(MASTER, "좋아요, 출발할까요")
		endif
		
	else
		CALL systemError("잘못된 입력입니다")
		clearLine 2
		goto inputCharacteristics
	endif

@customizeClothes

	PRINTL
	PRINTL 복장 선택
	PRINTL [1] - 평상복 스타트
	PRINTL [2] - 그 외의 입력은 전부 속옷만 입고 시작함 ㅅㄱ

	input
	
	if RESULT == 1
		PRINTL 정상적으로 옷 입고 스타트
		ITEM:1201 = 1
		ITEM:1301 = 1
		ITEM:1401 = 1
	
		CALL changeMasterEquips(12, 1201) ; 하얀셔츠
		CALL changeMasterEquips(13, 1301) ; 스커트
		CALL changeMasterEquips(14, 1401) ; 운동화
	else
		PRINTL 옷이 없는 대신 돈 500원을 추가
		MONEY += 500
	endif

	ITEM:1501 = 1
	CALL changeMasterEquips(15, 1501) ; 보통속옷
